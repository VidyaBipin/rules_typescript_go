# Copyright 2017 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@build_bazel_rules_typescript//:defs.bzl", "ts_library")
load("@build_bazel_rules_nodejs//:defs.bzl", "nodejs_binary")

ts_library(
    name = "tslint_runner",
    srcs = ["tslint_runner.ts"],
    deps = ["//internal/tsc_wrapped"],
    tsconfig = ":tsconfig.json",
)

nodejs_binary(
    name = "tslint_analyzer",
    entry_point = "build_bazel_rules_typescript/tslint/tslint_runner.js",
    data = [
        ":tslint_runner",

        # Should be @bazel_tools//src/main/protobuf:*.proto
        # see https://github.com/bazelbuild/bazel/issues/3155#issuecomment-308156976
        "//internal:extra_actions_base.proto",
        "//internal:worker_protocol.proto",
        ":tslint.json",
    ],
)

# Defines an action that shadows a ts_library action.
# The action execution runs the nodejs_binary above.
# It receives the same inputs as the shadowed rule. These inputs are provided
# in the first argument, which is an EXTRA_ACTION_FILE that contains an extra_action proto.
# It outputs to a specially named file, using the special ACTION_ID variable.
# See https://docs.bazel.build/versions/master/be/extra-actions.html
extra_action(
    name = "tslint_action",
    cmd = "$(location :tslint_analyzer) $(EXTRA_ACTION_FILE) $(output $(ACTION_ID))",
    out_templates = ["$(ACTION_ID)"],
    tools = [":tslint_analyzer"],
)

# Registers the extra action to run on any actions that have the
# "TypeScriptCompile" mnemonic.
action_listener(
    name = "tslint",
    extra_actions = [":tslint_action"],
    mnemonics = [
        "TypeScriptCompile",
    ],
    visibility = ["//visibility:public"],
)
